plugins {
    id 'java-library'
    id 'jvm-test-suite'
    id 'maven-publish'
    id "com.diffplug.spotless" version "5.8.2"
}

group 'org.quiltmc'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    api "com.google.code.gson:gson:2.8.9"
    api 'org.jetbrains:annotations:20.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

java {
    withSourcesJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks {
    compileJava {
        javaCompiler = javaToolchains.compilerFor {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
    jar {
        from ('.') {
            include 'LICENSE'
        }
    }
}

testing {
    //noinspection GroovyAssignabilityCheck
    suites {
        test {
            useJUnitJupiter()

            targets.all {
                testTask.configure {
                    testLogging {
                        events "passed", "skipped", "failed"
                    }
                }
            }
        }
    }
}

spotless {
    java {
        // Use comma separator for openjdk like license headers
        licenseHeaderFile(file("HEADER")).yearSeparator(", ")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.quiltmc'
            artifactId = 'launchermeta-parser'
            version = project.version
            from components.java
        }
    }
    repositories {
        def ENV = System.getenv()

        if (ENV.MAVEN_URL) {
            maven {
                url = ENV.MAVEN_URL

                credentials {
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}