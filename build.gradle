plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.diffplug.spotless" version "5.8.2"
}

group 'org.quiltmc'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    api "com.google.code.gson:gson:2.8.9"
    api 'org.jetbrains:annotations:20.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
}

jar {
    from ('.') {
        include 'LICENSE'
    }
}

tasks.withType(JavaCompile.class) {
    it.targetCompatibility = 16
    it.sourceCompatibility = 16
}

tasks.getByName("compileJava") {
    it.targetCompatibility = 8
    it.sourceCompatibility = 8
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

spotless {
    java {
        // Use comma separator for openjdk like license headers
        licenseHeaderFile(file("HEADER")).yearSeparator(", ")
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'org.quiltmc'
            artifactId = 'launchermeta-parser'
            version = project.version
            from components.java
        }
    }
    repositories {
        def ENV = System.getenv()

        if (ENV.MAVEN_URL) {
            maven {
                url = ENV.MAVEN_URL

                credentials {
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                }
            }
        }
    }
}